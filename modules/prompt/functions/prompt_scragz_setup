# Prompt
function prompt_scragz_precmd() {
  # last command exit status smiley, hidden if iterm2 integration present
  PROMPT_SMILEY='%(?,%F{green}☺%F{none},%F{red}☻%F{none})'

  if (( $+functions[git-info] )); then
    git-info
  fi

  # set a simple variable to show when in screen
  if [[ -n "${WINDOW}" ]]; then
    SCREEN=" S:${WINDOW}"
  else
    SCREEN=""
  fi

  # check if jobs are executing
  if [[ $(jobs | wc -l) -gt 0 ]]; then
    JOBS=" J:%j"
  else
    JOBS=""
  fi

  if [ -n "$SSH_CLIENT" ] || [ -n "$SSH_TTY" ]; then
    PROMPT_HOST='%F{magenta}%m'
  else
    PROMPT_HOST='%F{cyan}%m'
  fi

  #if typeset -f iterm2_prompt_mark > /dev/null; then
  #  PROMPT_SMILEY=''
  #else
  alias iterm2_prompt_mark=:
  #fi
}

function prompt_scragz_setup() {
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_scragz_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' alternate ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{red}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{green}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt'  '%F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='
%(!.%B%F{red}%n.%f%b%n)%F{blue}@${PROMPT_HOST}%f%b %F{yellow}${PWD/#$HOME/~} ${git_info:+${(e)git_info[prompt]}}
%{$(iterm2_prompt_mark)%}${PROMPT_SMILEY}%f%b '
  RPROMPT='%F{yellow}!%h${SCREEN}${JOBS}%(?.. E:%?)%f${git_rprompt_info}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_scragz_setup "$@"
